group 'me.ddevil'
version '1.0.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.10'
    ext.main_repo = "http://repo.imddevil.com/repository/maven-snapshots/"
    ext.mirai_module = "miraiModule"
    ext.discord_token = "discord_token"
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

def rootBuildDir = new File(rootDir, "output")
allprojects {
    afterEvaluate {
        if (ext.has(mirai_module) && (boolean) ext.get(mirai_module)) {
            it.buildDir = new File(rootBuildDir, "plugins/$it.name")
            repositories {
                mavenCentral()
                jcenter()
                maven {
                    url = "http://repo.imddevil.com/repository/maven-snapshots/"
                }
            }
            dependencies {
                compile project(':bot')
            }
            compileKotlin {
                kotlinOptions {
                    jvmTarget = "1.8"
                }
            }
            compileTestKotlin {
                kotlinOptions {
                    jvmTarget = "1.8"
                }
            }
            if (properties.containsKey(discord_token)) {
                test {
                    systemProperty(discord_token, properties[discord_token])
                }
            }
        }
    }
}